<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml"
		paddingTop="10"
		paddingLeft="0"
	        paddingRight="0"
	        paddingBottom="10"
		creationComplete="initVars()" >	
  <mx:Script>
	<![CDATA[
		import mx.controls.Alert;
		import NetConnectionClient;
 		import flash.events.NetStatusEvent;
		import mx.events.ListEvent;
		import mx.events.VideoEvent;
 		
 		public var nc:NetConnection;
		public var nsPublish:NetStream;   

                // Parameters assigned from flash vars
                public var rtmp_url:String;
                public var token:String;

		private var camera:Camera;

		// Register callback to expose function to javascript
		//ExternalInterface.addCallback("initCamera", initCamera);
		//ExternalInterface.addCallback("startPublishing", startPublishing);
		
		// Assign values to new properties.
		private function initVars():void {
			rtmp_url = mx.core.FlexGlobals.topLevelApplication.parameters.rtmp_url;
			token = mx.core.FlexGlobals.topLevelApplication.parameters.id;
			// Register callback to expose function to javascript
			ExternalInterface.addCallback("startPublishing", startPublishing);
			ExternalInterface.addCallback("stopPublishing", stopPublishing);
			initCamera();
		}	
		//Attaching Camera to the VideoDisplay
		private function initCamera():void {
			camera = Camera.getCamera();
			configureCamera();
		}

		private function changeCamera(evt:ListEvent):void {
			//var tList:List = evt.currentTarget as List;
			//var cameraName:String = tList.selectedIndex.toString();
			var cameraName:String = evt.currentTarget.selectedIndex.toString();
			camera = Camera.getCamera(cameraName);
			configureCamera();
			pubVideo.attachCamera(camera);
			if (nc.connected) {
				nsPublish.attachCamera(camera);
			}
		}

		private function configureCamera():void {
			camera.setMode(320, 240, 30, false);
                        camera.setQuality(0, 88);
                        camera.setKeyFrameInterval(15); 
                        pubVideo.attachCamera(camera);
		}

		// Initializing Connection and invoking publishStream
		private function startPublishing():void{
			if (camera == null) {
				initCamera();
			}
			fadeIn.play([rec]);
			nc = new NetConnection();
			nc.client = new NetConnectionClient();	
			nc.objectEncoding = flash.net.ObjectEncoding.AMF0;	      			
			nc.connect("rtmp://" + rtmp_url + "?id=" + token); //Path to FMS Server e.g. rtmp://<hostname>/<application name>
			
			nc.addEventListener("netStatus", publishStream);	//Listener to see if connection is successful
		}

		// Stop publishing now
		private function stopPublishing():void{
			camera = null;
			nc.close();
			fadeOut.play([rec]);
			pubVideo.attachCamera(null);
			//pubVideo.mx_internal::videoPlayer.clear();
		}

		private function publishStream(event:NetStatusEvent):void{
			if(nc.connected){
				nsPublish = new NetStream(nc);  //Initializing NetStream
				nsPublish.attachCamera(camera);
				nsPublish.attachAudio(Microphone.getMicrophone()); //Attaching Camera & Microphone
				nsPublish.publish("live"); //Publish stream
			}
			else {
				// Only show error if camera is active
				if (camera != null) {
					mx.controls.Alert.show("Connection Error");
				}
			}
		}

		private function showControls():void {
			fadeIn.play([controls]);
		}
 
		private function hideControls():void {
			fadeOut.play([controls]);
		}
	]]>
  </mx:Script>

  <mx:Style>
        .controllerStyle {
            bottom: 5;
            left: 5;
            right: 5;
            paddingBottom: 5;
            paddingLeft: 5;
            paddingRight: 5;
            paddingTop: 5;
            alpha: 0;
            background-color: #000000;
            background-alpha: 0.5;
        }
        .recStyle {
	    left: 5;
	    top: 5;
	    alpha: 0;
	    background-color: #000000;
	    background-alpha: 0.5;
            color: #FF0000;
	    fontWeight: "bold";
        }
  </mx:Style>

  <mx:Fade id="fadeIn" alphaFrom="0.0" alphaTo="1.0" />
  <mx:Fade id="fadeOut" alphaFrom="1.0" alphaTo="0.0" />

  <mx:Canvas rollOver="showControls()" rollOut="hideControls()">
    <mx:VideoDisplay width="320" height="240" id="pubVideo"/>
    <mx:Label id="rec" styleName="recStyle" text="LIVE" alpha="0.0"/>
    <mx:HBox id="controls" styleName="controllerStyle" alpha="0.0">
      <mx:ComboBox id="cameras_cb" dataProvider="{Camera.names}" change="changeCamera(event)"/> 
    </mx:HBox>
  </mx:Canvas>

  <!--<mx:Button label="publish Stream" click="startPublishing()"/>
  <mx:Button label="stop Stream" click="stopPublishing()"/>-->

</mx:Application>
